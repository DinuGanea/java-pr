// Project version (effectively milestone)
version '1.0'

// Neo4j version
def neo4jVersion = "3.0.0"

// Object-Graph mapping version
def ogmVersion = "2.0.4"

apply plugin: 'java'

// Min. java version needed to run the project
sourceCompatibility = 1.8

// Define manifest attributes (no need to create a separate file)
jar {
    manifest {
        attributes(
            'Implementation-Version': version,
            'Main-Class': 'Main'
        )
    }
}

// Specify the task to build the jar archive and make sure not to get any conflicts with any other meta-inf files
task fatJar(type: Jar) {
    manifest.from jar.manifest
    from {
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

// Specify the artifact to build
artifacts {
    archives fatJar
}

// Resolve all
repositories {
    mavenCentral()
}

// Add here the dependencies
dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.7'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.7'
    compile "org.neo4j:neo4j-ogm-core:${ogmVersion}"
    compile "org.neo4j:neo4j-ogm-embedded-driver:${ogmVersion}"
    compile 'org.jsoup:jsoup:1.10.1'

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-simple
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.1'

}
